plugins {
    id "base"
}

buildDir = "dist"

task installCodeEngine(type: Copy, dependsOn: [":code-engine:distTar"]) {
    from tarTree("../code-engine/build/distributions/code-engine.tar.gz")
    into "static/local"
}

task installCodeEngineModels(type: Copy, dependsOn: [":code-engine:packageModels"]) {
    from tarTree("../code-engine/build/distributions/code-engine-models.tar.gz")
    into "static/local"
}

task installCore(type: Copy, dependsOn: [":core:distTar"]) {
    from tarTree("../core/build/distributions/core.tar.gz")
    into "static/local"
    rename '^core$', 'serenade-core'
}

task installJdk(type: Copy, dependsOn: [":packageJdk"]) {
    from tarTree("../build/distributions/jdk.tar.gz")
    into "static/local"
}

task installSpeechEngine(type: Copy, dependsOn: [":speech-engine:distTar"]) {
    from tarTree("../speech-engine/build/distributions/speech-engine.tar.gz")
    into "static/local"
}

task installSpeechEngineModels(type: Copy, dependsOn: [":speech-engine:packageModels"]) {
    from tarTree("../speech-engine/build/distributions/speech-engine-models.tar.gz")
    into "static/local"
}

task installServer(dependsOn: [
    installCodeEngine,
    installCodeEngineModels,
    installCore,
    installJdk,
    installSpeechEngine,
    installSpeechEngineModels
]) {}

task initElectron(type: Exec) {
    // do not build if folder node_modules exists

    if (file("node_modules").exists()) {
        println "node_modules exists, skipping npm install"
        // Gradle exec type requires commandLine to be always called, so we're calling a dummy here
        commandLine "bash", "-c", "true"
    } else {
        println "Running npm install"
        commandLine "bash", "-c", "./bin/build.py"
    }
}

task clientDistLinux(type: Exec, dependsOn: [":client:initElectron"]) {
    commandLine "npm", "run", "package:dist-linux"
}

task fullDistLinux(type: Exec, dependsOn: [":client:initElectron", "installServer"]) {
    commandLine "npm", "run", "package:dist-linux"
}

task clientDistMac(type: Exec, dependsOn: [":client:initElectron"]) {
    commandLine "npm", "run", "package:dist-mac"
}

task clientDistWindows(type: Exec, dependsOn: [":client:initElectron"]) {
    commandLine "npm", "run", "package:dist-win"
}

clean {
    delete "static/local"
    delete "node_modules"
}